plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
}

def libraryVersion = '1.1.5'  // vedi versionName + versionCode

android {
    compileSdkVersion 35

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 35

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {

        codedebug {
            minifyEnabled false
            testCoverageEnabled false
            buildConfigField("String", "VERSION_NAME", "\"${defaultConfig.versionName}\"")
        }
        debug {
            minifyEnabled false
            testCoverageEnabled true
            buildConfigField("String", "VERSION_NAME", "\"${defaultConfig.versionName}\"")
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField("String", "VERSION_NAME", "\"${defaultConfig.versionName}\"")
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = "17" // or "1.8" if you need backward compatibility
    }

    buildFeatures {
        buildConfig true
        viewBinding true
    }
    namespace 'net.aliaslab.securecall.flexqrreader'
}

dependencies {

    implementation 'org.jetbrains.kotlin:kotlin-stdlib:1.8.20'
    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'com.google.android.material:material:1.4.0'

    implementation 'androidx.preference:preference-ktx:1.2.0'

    // zebra
    implementation 'com.google.zxing:core:3.4.1'

    implementation 'com.google.android.gms:play-services-auth:20.4.1'
    implementation 'com.google.android.gms:play-services-auth-api-phone:18.0.1'
  /*  implementation 'com.google.android.gms:play-services-vision:17.0.2', {
        exclude group: 'com.android.support'
    }*/
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    implementation 'com.journeyapps:zxing-android-embedded:4.3.0'


    // CameraX core library
    def camerax_version = '1.5.1'
    implementation "androidx.camera:camera-core:$camerax_version"

    // CameraX Camera2 extensions
    implementation "androidx.camera:camera-camera2:$camerax_version"

    // CameraX Lifecycle library
    implementation "androidx.camera:camera-lifecycle:$camerax_version"

    // CameraX View class
    implementation "androidx.camera:camera-view:$camerax_version"

    // CameraX Extensions library
    implementation "androidx.camera:camera-extensions:$camerax_version"

    //WindowManager
    implementation 'androidx.window:window:1.0.0'

    implementation 'com.google.mlkit:barcode-scanning:17.3.0'

    def lifecycle_version = "2.5.1"
    implementation "androidx.lifecycle:lifecycle-viewmodel:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
}

publishing {
    publications {
        aar(MavenPublication) {
            groupId 'net.aliaslab'
            version libraryVersion
            def debugAsset = "${project.getName()}-DEBUG"
            def snapshotAsset = "${project.getName()}-SNAP"
            def asset = libraryVersion.endsWith('SNAP') ? snapshotAsset : libraryVersion.endsWith('DEBUG') ? debugAsset : project.getName()
            artifactId(asset)

            if (libraryVersion.endsWith('DEBUG')) {
                artifact("$buildDir/outputs/aar/${project.getName()}-codedebug.aar")
            } else {
                artifact("$buildDir/outputs/aar/${project.getName()}-release.aar")
            }

            pom.withXml {
                def dependencies = asNode().appendNode('dependencies')
                configurations.getByName("releaseCompileClasspath").getResolvedConfiguration().getFirstLevelModuleDependencies().each {
                    def dependency = dependencies.appendNode('dependency')
                    dependency.appendNode('groupId', it.moduleGroup)
                    dependency.appendNode('artifactId', it.moduleName)
                    dependency.appendNode('version', it.moduleVersion)
                }
            }
        }
    }

    repositories {
        maven {
            name = 'trust-mobile-m2'
            url = "https://nexus.ts-paas.com/repository/trust-mobile-m2"
            credentials {
                username project.findProperty('nexusUser') ?: 'user'
                password project.findProperty('nexusPassword') ?: 'password'
            }
        }
    }
}
